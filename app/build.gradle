apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "de.mannodermaus.android-junit5"

apply plugin: 'jacoco'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.concrete.tentacle"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"https://tentacle-backend.herokuapp.com/"'
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://tentacle-backend.herokuapp.com/"'
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

androidExtensions {
    experimental = true
}

jacoco {
    toolVersion = "$jacobVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*' ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.google.android.material:material:$material"
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraitLayout"

    //UI Components
    //https://github.com/MdFarhanRaja/SearchableSpinner
    implementation "com.github.MdFarhanRaja:SearchableSpinner:$searchableSpinner"

    //Test
    testImplementation "junit:junit:$junit"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit5"
    androidTestImplementation "androidx.test:runner:$runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$expresso"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5"

    //Koin
    implementation "org.koin:koin-core:$koinVersion"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"
    testImplementation "org.koin:koin-test:$koinVersion"

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$retrofitLogging"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitAdapterRxJava"


    implementation "com.squareup.okhttp3:okhttp:3.10.0"

    //Observable
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid"
    implementation "io.reactivex.rxjava2:rxjava:$rxJava"

    //AndroidX ViewModel/LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle"
    implementation "com.google.android.material:material:$androidMaterial"
    
    //Utils
    implementation "br.com.concrete:canarinho:$canarinho"
}
